// SPDX-License-Identifier: GPL-3.0-or-later WITH LicenseRef-cardkit-ads-exception
plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.kover)
}

// Pure Kotlin: the authoritative rules of Euchre. No Android dependency, so it is fully
// unit-testable, runs in the browser (wasmJs), and can run server-side for online multiplayer.
kotlin {
    jvmToolchain(21)
    jvm()
    wasmJs {
        browser()
    }

    sourceSets {
        commonMain.dependencies {
            api(libs.cardkit.core)
            api(libs.kotlinx.serialization.core)
        }
        jvmTest.dependencies {
            implementation(kotlin("test"))
            implementation(libs.junit.jupiter)
            implementation(libs.kotlinx.coroutines.test)
            implementation(libs.kotlinx.serialization.json) // EuchreState snapshot round-trip test
            runtimeOnly(libs.junit.platform.launcher)
        }
    }
}

tasks.withType<Test>().configureEach {
    useJUnitPlatform()
}

// Coverage ratchet for the pure rules engine (same bar as 500's engine).
kover {
    reports {
        verify {
            rule {
                minBound(90)
            }
        }
    }
}
